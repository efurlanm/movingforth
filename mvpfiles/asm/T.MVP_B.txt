*/ MVP/B  9/20/82
*
*    '!' ( N ADDR --- )
*
         HEX   81
         ASC   "!"
         HEX   0000
STORE    DA    STORE+2
         LDA   $2,X
         STA   (0,X)      STORE SECOND 16 BIT VALUE ON STACK
         INC   $0,X       TO MEMORY AS ADDRESSED BY
         BNE   STORE1     BOTTOM OF STACK.
         INC   $1,X
STORE1   LDA   $3,X
         STA   (0,X)
         JMP   POPTWO
*
*    '#'  ( UD1 --- UD2 )
*
         HEX   81
         ASC   "#"
         DA    STORE-4
SHARP    DA    DOCOLON
         DA    BASE
         DA    AT
         DA    MSLMOD
         DA    ROT
         DA    LIT
         HEX   0900
         DA    OVER
         DA    LESS
         DA    ZBRANCH
         DFB   SHARP1-*,00
         DA    LIT
         HEX   0700
         DA    PLUS
SHARP1   DA    LIT
         HEX   3000
         DA    PLUS
         DA    HOLD
         DA    EXIT
*
*    '#>'  ( UD --- ADDR N )
*
         HEX   82
         DCI   '#>'
         DA    SHARP-4
SHARPA   DA    DOCOLON
         DA    DDROP
         DA    HLD
         DA    AT
         DA    PAD
         DA    OVER
         DA    SUB
         DA    EXIT
*
*   '#BUFF'  ( --- N )
*
         HEX   85
         DCI   '#BUFF'
         DA    SHARPA-5
NUMBUFF  DA    DOCONST
         HEX   0200
*
*    '#S'  ( UD --- 0 0 )
*
         HEX   82
         DCI   '#S'
         DA    NUMBUFF-8
SHARPS   DA    DOCOLON
SHARPS1  DA    SHARP
         DA    DDUP
         DA    OR
         DA    ZEQUAL
         DA    ZBRANCH
         DFB   SHARPS1-*,>SHARPS1-*
         DA    EXIT
*
*    '''  ( --- ADDR )
*
         HEX   C1
         ASC   !'!
         DA    SHARPS-5
TICK     DA    DOCOLON
         DA    DFIND
         DA    NOT
         DA    AABORTQ
         HEX   09
         ASC   'NOT FOUND'
         DA    DROP
         DA    LITERAL
         DA    EXIT
*
*    ''-FIND'  ( --- ADDR )
*
         HEX   86
         DCI   /'-FIND/
         DA    TICK-4
TDFIND   DA    DOUSER
         HEX   16
*
*    ''?TERMINAL'  ( --- ADDR )
*
         HEX   8A
         DCI   /'?TERMINAL/
         DA    TDFIND-9
TQTERM   DA    DOUSER
         HEX   18
*
*    ''ABORT'  ( --- ADDR )
*
         HEX   86
         DCI   /'ABORT/
         DA    TQTERM-13
TABORT   DA    DOUSER
         HEX   1A
*
*    ''BLOCK'  ( --- ADDR )
*
         HEX   86
         DCI   /'BLOCK/
         DA    TABORT-9
TBLOCK   DA    DOUSER
         HEX   1C
*
*    ''CR'  ( --- ADDR )
*
         HEX   83
         DCI   /'CR/
         DA    TBLOCK-9
TCR      DA    DOUSER
         HEX   1E
*
*    ''EMIT'  ( --- ADDR )
*
         HEX   85
         DCI   /'EMIT/
         DA    TCR-6
TEMIT    DA    DOUSER
         HEX   20
*
*    ''EXPECT'  ( --- ADDR )
*
         HEX   87
         DCI   /'EXPECT/
         DA    TEMIT-8
TEXPECT  DA    DOUSER
         HEX   22
*
*    ''INTERPRET'
*
         HEX   8A
         DCI   /'INTERPRET/
         DA    TEXPECT-10
TINTERP  DA    DOUSER
         HEX   24
*
*    ''KEY'  ( --- ADDR )
*
         HEX   84
         DCI   /'KEY/
         DA    TINTERP-13
TKEY     DA    DOUSER
         HEX   26
*
*    ''LOAD'  ( --- ADDR )
*
         HEX   85
         DCI   /'LOAD/
         DA    TKEY-7
TLOAD    DA    DOUSER
         HEX   28
*
*    ''NUMBER'  ( --- ADDR )
*
         HEX   87
         DCI   /'NUMBER/
         DA    TLOAD-8
TNUMBER  DA    DOUSER
         HEX   2A
*
*    ''PAGE'  ( --- ADDR )
*
         HEX   85
         DCI   /'PAGE/
         DA    TNUMBER-10
TPAGE    DA    DOUSER
         HEX   2C
*
*    ''R/W'  ( --- ADDR )
*
         HEX   84
         DCI   *'R/W*
         DA    TPAGE-8
TRSLW    DA    DOUSER
         HEX   2E
*
*    ''SAVE-FORTH'   ( --- ADDR )
*
         HEX   8B
         DCI   /'SAVE-FORTH/
         DA    TRSLW-7
TSVFRTH  DA    DOUSER
         HEX   30
*
*    ''STREAM'  ( --- ADDR )
*
         HEX   87
         DCI   /'STREAM/
         DA    TSVFRTH-14
TSTREAM  DA    DOCOLON
         DA    BLK
         DA    AT
         DA    QDUP
         DA    ZBRANCH
         DFB   TSTREA1-*,00
         DA    BLOCK
         DA    BRANCH
         DFB   TSTREA2-*,00
TSTREA1  DA    TIB
         DA    AT
TSTREA2  DA    TOIN
         DA    AT
         DA    PLUS
         DA    EXIT
*
*    ''VOCABULARY'  ( --- ADDR )
*
         HEX   8B
         DCI   /'VOCABULARY/
         DA    TSTREAM-10
TVOCABU  DA    DOUSER
         HEX   32
*
*    ''WORD'  ( --- ADDR )
*
         HEX   85
         DCI   /'WORD/
         DA    TVOCABU-14
TWORD    DA    DOUSER
         HEX   34
*
*    '('
*
         HEX   C1
         ASC   "("
         DA    TWORD-8
PAREN    DA    DOCOLON
         DA    LIT
         HEX   FFFF
         DA    TOIN
         DA    PSTORE
         DA    LIT
         HEX   2900
         DA    WORD
         DA    CAT
         DA    ONEP
         DA    HERE
         DA    PLUS
         DA    CAT
         DA    LIT
         HEX   2900
         DA    EQUAL
         DA    NOT
         DA    QSTREAM
         DA    EXIT
*
*    '*'  ( N1 N2 --- N3 )
*
         HEX   81
         ASC   "*"
         DA    PAREN-4
STAR     DA    DOCOLON
         DA    USTAR
         DA    DROP
         DA    EXIT
*
*    '*/' ( N1 N2 N3 --- N4 )
*
         HEX   82
         DCI   '*/'
         DA    STAR-4
STARSL   DA    DOCOLON
         DA    SSLMOD
         DA    SWAP
         DA    DROP
         DA    EXIT
*
*    '*/MOD'  ( N1 N2 N3 --- N4 N5 )
*
         HEX   85
         DCI   '*/MOD'
         DA    STARSL-5
SSLMOD   DA    DOCOLON
         DA    TOR
         DA    MSTAR
         DA    RFROM
         DA    MSL
         DA    EXIT
*
*    '+' ( N1 N2 --- SUM )
*
         HEX   81
         ASC   "+"
         DA    SSLMOD-8
PLUS     DA    PLUS+2
         CLC
         LDA   $0,X
         ADC   $2,X
         STA   $2,X
         LDA   $1,X
         ADC   $3,X
         STA   $3,X
         INX
         INX
         JMP   NEXT
*
*    '+!' ( N ADDR --- )
*
         HEX   82
         DCI   '+!'
         DA    PLUS-4
PSTORE   DA    PSTORE+2
         CLC
         LDA   (0,X)      FETCH 16 BIT VALUE ADDRESSES
         ADC   $2,X       BY BOTTOM OF STACK, ADDING TO
         STA   (0,X)      SECOND ITEM ON STACK, AND
         INC   $0,X       RETURN TO MEMORY.
         BNE   PSTORE1
         INC   $1,X
PSTORE1  LDA   (0,X)
         ADC   $3,X
         STA   (0,X)
         JMP   POPTWO
*
*    '+-'  ( N1 2N --- N3 )
*
         HEX   82
         DCI   '+-'
         DA    PSTORE-5
PM       DA    DOCOLON
         DA    ZLESS
         DA    ZBRANCH
         DFB   PM1-*,00
         DA    NEGATE
PM1      DA    EXIT
*
*    '+BUF'  ( ADDR1 --- ADDR2 F )
*
         HEX   84
         DCI   '+BUF'
         DA    PM-5
PBUF     DA    DOCOLON
         DA    LIT
         HEX   0404       ;DISK BUFFER SIZE
         DA    PLUS
         DA    DUP
         DA    LIMIT
         DA    EQUAL
         DA    ZBRANCH
         DFB   PBUF1-*,00
         DA    DROP
         DA    FIRST
PBUF1    DA    DUP
         DA    PREV
         DA    AT
         DA    SUB
         DA    EXIT
*
*    '+LOOP'  ( N --- )
*
         HEX   C5
         DCI   '+LOOP'
         DA    PBUF-7
PLOOP    DA    DOCOLON
         DA    LIT
         HEX   0300
         DA    QPAIRS
         DA    COMPILE
         DA    APLOOPA
         DA    HERE
         DA    SUB
         DA    COMMA
         DA    EXIT
*
*    ','  ( N --- )
*
         HEX   81
         ASC   ","
         DA    PLOOP-8
COMMA    DA    DOCOLON
         DA    HERE
         DA    STORE
         DA    LIT
         HEX   0200
         DA    ALLOT
         DA    EXIT
*
*    '-'  ( N1 N2 --- DIFF )
*
         HEX   81
         ASC   "-"
         DA    COMMA-4
SUB      DA    SUB+2
         SEC
         TYA
         SBC   $0,X
         STA   $0,X
         TYA
         SBC   $1,X
         STA   $1,X
         CLC
         LDA   $0,X
         ADC   $2,X
         STA   $2,X
         LDA   $1,X
         ADC   $3,X
         STA   $3,X
         INX
         INX
         JMP   NEXT
*
*    '-FIND' ( --- PFA B TF ) IF FOUND
*            ( --- FF ) IF NOT FOUND
         HEX   85
         DCI   '-FIND'
         DA    SUB-4
DFIND    DA    DOCOLON
         DA    TDFIND
         DA    AT
         DA    EXECUTE
         DA    EXIT
*
*    '-TRAILING'  ( ADDR N1 --- ADDR N2 )
*
         HEX   89
         DCI   '-TRAILING'
         DA    DFIND-8
DTRAIL   DA    DOCOLON
         DA    DUP
         DA    ZERO
         DA    ADOA
DTRAIL1  DA    DDUP
         DA    PLUS
         DA    ONE
         DA    SUB
         DA    CAT
         DA    BL
         DA    SUB
         DA    ZBRANCH
         DFB   DTRAIL2-*,00
         DA    LEAVE
         DA    BRANCH
         DFB   DTRAIL3-*,00
DTRAIL2  DA    ONE
         DA    SUB
DTRAIL3  DA    ALOOPA
         DFB   DTRAIL1-*,>DTRAIL1-*
         DA    EXIT
*
*    '.'  ( N --- )
*
         HEX   81
         ASC   "."
         DA    DTRAIL-12
DOT      DA    DOCOLON
         DA    STOD
         DA    DDOT
         DA    EXIT
*
*    '."'
*
         HEX   C2
         DCI   /."/
         DA    DOT-4
DOTQ     DA    DOCOLON
         DA    TSTREAM
         DA    CAT
         DA    LIT
         HEX   2200
         DA    EQUAL
         DA    ZBRANCH
         DFB   DOTQ1-*,00
         DA    ONE
         DA    TOIN
         DA    PSTORE
         DA    BRANCH
         DFB   DOTQ4-*,00
DOTQ1    DA    LIT
         HEX   2200
         DA    STATE
         DA    AT
         DA    ZBRANCH
         DFB   DOTQ2-*,00
         DA    COMPILE
         DA    ADOTQA
DOTQ2    DA    WORD
         DA    DUP
         DA    CAT
         DA    ONEP
         DA    OVER
         DA    PLUS
         DA    CAT
         DA    LIT
         HEX   2200
         DA    EQUAL
         DA    NOT
         DA    QSTREAM
         DA    STATE
         DA    AT
         DA    ZBRANCH
         DFB   DOTQ3-*,00
         DA    CAT
         DA    ONEP
         DA    ALLOT
         DA    BRANCH
         DFB   DOTQ4-*,00
DOTQ3    DA    COUNT
         DA    TYPE
DOTQ4    DA    EXIT
*
*    '.LINE'  ( LINE SCR --- )
*
         HEX   85
         DCI   '.LINE'
         DA    DOTQ-5
DOTLINE  DA    DOCOLON
         DA    ALINEA
         DA    DTRAIL
         DA    TYPE
         DA    EXIT
*
*    '.R'  ( N1 N2 --- )
*
         HEX   82
         DCI   '.R'
         DA    DOTLINE-8
DOTR     DA    DOCOLON
         DA    TOR
         DA    STOD
         DA    RFROM
         DA    DDOTR
         DA    EXIT
*
*    '/'  ( N1 N2 --- N3 )
*
         HEX   81
         ASC   "/"
         DA    DOTR-5
SLASH    DA    DOCOLON
         DA    SLMOD
         DA    SWAP
         DA    DROP
         DA    EXIT
*
*    '/LOOP'  ( N --- )
*
         HEX   C5
         DCI   '/LOOP'
         DA    SLASH-4
UPLOOP   DA    DOCOLON
         DA    LIT
         HEX   0300
         DA    QPAIRS
         DA    COMPILE
         DA    AUPLOOP
         DA    HERE
         DA    SUB
         DA    COMMA
         DA    EXIT
*
*    '/MOD'  (' N1 N2 --- N3 N4 )
*
         HEX   84
         DCI   '/MOD'
         DA    UPLOOP-8
SLMOD    DA    DOCOLON
         DA    TOR
         DA    STOD
         DA    RFROM
         DA    MSL
         DA    EXIT
