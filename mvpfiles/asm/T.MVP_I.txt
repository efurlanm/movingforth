*/ MVP/I 2/20/83
*
*    'S->D'  ( N --- D )
*
         HEX   84
         DCI   'S->D'
         DA    RPSTORE-6
STOD     DA    DOCOLON
         DA    DUP
         DA    ZLESS
         DA    NEGATE
         DA    EXIT
*
*    'S0'  ( --- ADDR )
*
         HEX   82
         DCI   'S0'
         DA    STOD-7
SZERO    DA    DOCOLON
         DA    SPZERO
         DA    AT
         DA    EXIT
*
*    'SAVE-BUFFERS'  ( --- )
*
         HEX   8C
         DCI   'SAVE-BUFFERS'
         DA    SZERO-5
SAVEBUF  DA    DOCOLON
         DA    NUMBUFF
         DA    ONEP
         DA    ZERO
         DA    ADOA
SAVEBU1  DA    LIT
         HEX   FF7F
         DA    BUFFER
         DA    DROP
         DA    ALOOPA
         DFB   SAVEBU1-*,>SAVEBU1-*
         DA    EXIT
*
*    'SAVE-FORTH'  ( --- )
*
         HEX   8A
         DCI   'SAVE-FORTH'
         DA    SAVEBUF-15
SVFORTH  DA    DOCOLON
         DA    TSVFRTH
         DA    AT
         DA    EXECUTE
         DA    EXIT
*
*    'SCR'
*
         HEX   83
         DCI   'SCR'
         DA    SVFORTH-13
SCR      DA    DOUSER
         HEX   4E
*
*    'SEC/BLK'  ( --- N )
*
         HEX   87
         DCI   'SEC/BLK'
         DA    SCR-6
SECPBLK  DA    DOCREAT
         HEX   0400
         HEX   0800
         HEX   0800
         HEX   0800
         HEX   0800
         HEX   0800
         HEX   0800
*
*    'SEC/TR'  ( --- ADDR )
*
         HEX   86
         DCI   'SEC/TR'
         DA    SECPBLK-10
SECPTR   DA    DOCREAT
         HEX   1000
         HEX   2600
         HEX   2600
         HEX   5200
         HEX   5200
         HEX   6400
         HEX   6400
*
*    'SET-DRX'  ( N --- )
*
         HEX   87
         DCI   'SET-DRX'
         DA    SECPTR-9
SETDDRX  DA    DOCOLON
         DA    DRDDEN
         DA    DENSITY
         DA    STORE
         DA    BPDRV
         DA    OFFSET
         DA    PSTORE
         DA    EXIT
*
*    'SIGN'  ( N --- )
*
         HEX   84
         DCI   'SIGN'
         DA    SETDDRX-10
SIGN     DA    DOCOLON
         DA    ZLESS
         DA    ZBRANCH
         DFB   SIGN1-*,00
         DA    LIT
         HEX   2D00
         DA    HOLD
SIGN1    DA    EXIT
*
*    'SIZE'  ( --- )
*
         HEX   84
         DCI   'SIZE'
         DA    SIGN-7
SIZE     DA    DOCOLON
         DA    PAD
         DA    LIT
         DA    ORIGN
         DA    SUB
         DA    ZERO
         DA    LIT
         HEX   0001
         DA    USLMOD
         DA    SWAP
         DA    ZBRANCH
         DFB   SIZE1-*,00
         DA    ONEP
SIZE1    DA    NPAGES
         DA    CSTORE
         DA    EXIT
*
*    'SMUDGE'  ( --- )
*
         HEX   86
         DCI   'SMUDGE'
         DA    SIZE-7
SMUDGE   DA    DOCOLON
         DA    LATEST
         DA    LIT
         HEX   2000
         DA    TOGGLE
         DA    EXIT
*
*    'SP!'
*
         HEX   83
         DCI   'SP!'
         DA    SMUDGE-9
SPSTORE  DA    SPSTORE+2
         LDY   #6
         LDA   (UAP),Y    LOAD DATA STACK POINTER (X-REG)
         TAX              ;FROM USER VARIABLE 'SP0'.
         JMP   NEXT
*
*    'SP0'  ( --- ADDR )
*
         HEX   83
         DCI   'SP0'
         DA    SPSTORE-6
SPZERO   DA    DOUSER
         HEX   06
*
*    'SP@' ( --- ADDR )
*
         HEX   83
         DCI   'SP@'
         DA    SPZERO-6
SPAT     DA    SPAT+2
         TXA
PUSH0A   PHA
         LDA   #0
         JMP   PUSH
*
*    'SPACE'  ( --- )
*
         HEX   85
         DCI   'SPACE'
         DA    SPAT-6
SPACE    DA    DOCOLON
         DA    BL
         DA    EMIT
         DA    EXIT
*
*    'SPACES'  ( N --- )
*
         HEX   86
         DCI   'SPACES'
         DA    SPACE-8
SPACES   DA    DOCOLON
         DA    ZERO
         DA    MAX
         DA    QDUP
         DA    ZBRANCH
         DFB   SPACES2-*,00
         DA    ZERO
         DA    ADOA
SPACES1  DA    SPACE
         DA    ALOOPA
         DFB   SPACES1-*,>SPACES1-*
SPACES2  DA    EXIT
*
*    'SPDRV'  ( --- N )
*
         HEX   85
         DCI   'SPDRV'
         DA    SPACES-9
SPDRV    DA    DOCOLON
         DA    BPDRV
         DA    SPBLK
         DA    STAR
         DA    EXIT
*
*    'SPBLK'  ( --- N )
*
         HEX   85
         DCI   'SPBLK'
         DA    SPDRV-8
SPBLK    DA    DOCOLON
         DA    DENSITY
         DA    AT
         DA    LIT
         HEX   0600
         DA    MIN
         DA    TWOSTAR
         DA    SECPBLK
         DA    PLUS
         DA    AT
         DA    EXIT
*
*    'SPT'  ( --- N )
*
         HEX   83
         DCI   'SPT'
         DA    SPBLK-8
SPT      DA    DOCOLON
         DA    DENSITY
         DA    AT
         DA    LIT
         HEX   0600
         DA    MIN
         DA    TWOSTAR
         DA    SECPTR
         DA    PLUS
         DA    AT
         DA    EXIT
*
*    'STATE'  ( --- ADDR )
*
         HEX   85
         DCI   'STATE'
         DA    SPT-6
STATE    DA    DOUSER
         HEX   50
*
*    'SWAP' ( N1 N2 --- N2 N1 )
*
         HEX   84
         DCI   'SWAP'
         DA    STATE-8
SWAP     DA    SWAP+2
         LDA   $2,X
         PHA
         LDA   $0,X       
         STA   $2,X
         LDA   $3,X
         LDY   $1,X
         STY   $3,X
         JMP   PUT
*
*    'TEXT'  ( C --- )
*
         HEX   84
         DCI   'TEXT'
         DA    SWAP-7
TEXT     DA    DOCOLON
         DA    HERE
         DA    CSLL
         DA    ONEP
         DA    BLANK
         DA    WORD
         DA    BL
         DA    OVER
         DA    DUP
         DA    CAT
         DA    PLUS
         DA    ONEP
         DA    CSTORE
         DA    PAD
         DA    CSLL
         DA    ONEP
         DA    CMOVE
         DA    EXIT
*
*    'THEN'  ( --- )
*
         HEX   C4
         DCI   'THEN'
         DA    TEXT-7
THEN     DA    DOCOLON
         DA    QCOMP
         DA    TWO
         DA    QPAIRS
         DA    HERE
         DA    OVER
         DA    SUB
         DA    SWAP
         DA    STORE
         DA    EXIT
*
*    'TIB'  ( --- ADDR )
*
         HEX   83
         DCI   'TIB'
         DA    THEN-7
TIB      DA    DOUSER
         HEX   0A
*
*    'TOGGLE' ( ADDR B --- )
*
         HEX   86
         DCI   'TOGGLE'
         DA    TIB-6
TOGGLE   DA    TOGGLE+2
         LDA   (2,X)      COMPLEMENT BITS IN MEMORY ADDRESS 
         EOR   $0,X       SECOND ON STACK, BY PATTERN
         STA   (2,X)      ON BOTTOM OF STACK.
         JMP   POPTWO
*
*    'TRAVERSE'  ( ADDR1 N --- ADDR2 )
*
         HEX   88
         DCI   'TRAVERSE'
         DA    TOGGLE-9
TRAVERS  DA    DOCOLON
         DA    SWAP
TRAVER1  DA    OVER
         DA    PLUS
         DA    LIT
         HEX   7F00
         DA    OVER
         DA    CAT
         DA    LESS
         DA    ZBRANCH
         DFB   TRAVER1-*,>TRAVER1-*
         DA    SWAP
         DA    DROP
         DA    EXIT
*
*    'TYPE'  ( ADDR N --- )
*
         HEX   84
         DCI   'TYPE'
         DA    TRAVERS-11
TYPE     DA    DOCOLON
         DA    DUP
         DA    ZGREAT
         DA    ZBRANCH
         DFB   TYPE2-*,00
         DA    OVER
         DA    PLUS
         DA    SWAP
         DA    ADOA
TYPE1    DA    I
         DA    CAT
         DA    EMIT
         DA    ONE
         DA    AUPLOOP
         DFB   TYPE1-*,>TYPE1-*
         DA    BRANCH
         DFB   TYPE3-*,00
TYPE2    DA    DDROP
TYPE3    DA    EXIT
*
*    'U*' ( U1 U2 --- UD )
*
         HEX   82
         DCI   'U*'
         DA    TYPE-7
USTAR    DA    USTAR+2
         LDA   $2,X
         STA   N
         STY   $2,X
         LDA   $3,X
         STA   N+1
         STY   $3,X
         LDY   #16        FOR 16 BITS
USTAR1   ASL   $2,X
         ROL   $3,X
         ROL   $0,X
         ROL   $1,X
         BCC   USTAR2
         CLC
         LDA   N
         ADC   $2,X
         STA   $2,X
         LDA   N+1
         ADC   $3,X
         STA   $3,X
         LDA   #0
         ADC   $0,X
         STA   $0,X
         LDA   #0
         ADC   $1,X
         STA   $1,X
USTAR2   DEY
         BNE   USTAR1
         JMP   NEXT
*
*    'U.'  ( UN --- )
*
         HEX   82
         DCI   'U.'
         DA    USTAR-5
UDOT     DA    DOCOLON
         DA    ZERO
         DA    DDOT
         DA    EXIT
*
*    'U/MOD' ( UD U1 --- U2 U3 )
*
         HEX   85
         DCI   'U/MOD'
         DA    UDOT-5
USLMOD   DA    USLMOD+2
         STY   N+1
         LDA   $4,X
         LDY   $2,X
         STY   $4,X
         ASL
         STA   $2,X
         LDA   $5,X
         LDY   $3,X
         STY   $5,X
         ROL
         STA   $3,X
         LDA   #16        FOR 16 BITS
         STA   N
USLMOD1  ROL   $4,X
         ROL   $5,X
         ROL   N+1
         SEC
         LDA   $4,X
         SBC   $0,X
         TAY
         LDA   $5,X
         SBC   $1,X
         PHA
         LDA   N+1
         SBC   #0
         PLA
         BCC   USLMOD2
         STY   $4,X
         STA   $5,X
USLMOD2  ROL   $2,X
         ROL   $3,X
         DEC   N
         BNE   USLMOD1
         JMP   POP
*
*    'U<'  ( UN1 UN2 --- F )
*
         HEX   82
         DCI   'U<'
         DA    USLMOD-8
ULESS    DA    DOCOLON
         DA    ZERO
         DA    SWAP
         DA    ZERO
         DA    DLESS
         DA    EXIT
*
*    'UNTIL'
*
         HEX   C5
         DCI   'UNTIL'
         DA    ULESS-5
UNTIL    DA    DOCOLON
         DA    ONE
         DA    QPAIRS
         DA    COMPILE
         DA    ZBRANCH
         DA    HERE
         DA    SUB
         DA    COMMA
         DA    EXIT
*
*    'UP'  ( --- ADDR )
*
         HEX   82
         DCI   'UP'
         DA    UNTIL-8
UP       DA    DOCONST
         DA    UAP
*
*    'UPDATE'  ( --- )
*
         HEX   86
         DCI   'UPDATE'
         DA    UP-5
UPDATE   DA    DOCOLON
         DA    PREV
         DA    AT
         DA    AT
         DA    LIT
         HEX   0080
         DA    OR
         DA    PREV
         DA    AT
         DA    STORE
         DA    EXIT
*
*    'USE'  ( --- ADDR )
*
         HEX   83
         DCI   'USE'
         DA    UPDATE-9
USE      DA    DOCREAT
         DA    FIRST
*
*    'USER'
*
         HEX   84
         DCI   'USER'
         DA    USE-6
USER     DA    DOCOLON
         DA    CONSTNT
         DA    ASCODEA
DOUSER   LDY   #2
         CLC
         LDA   (W),Y
         ADC   UAP
         PHA
         LDA   #0
         ADC   UAP+1
         JMP   PUSH
*
*    'VARIABLE'
*
         HEX   88
         DCI   'VARIABLE'
         DA    USER-7
VARIABL  DA    DOCOLON
         DA    CREATE
         DA    TWO
         DA    ALLOT
         DA    EXIT
*
*    'VOC-LINK'  ( --- ADDR )
*
         HEX   88
         DCI   'VOC-LINK'
         DA    VARIABL-11
VOCLINK  DA    DOUSER
         HEX   14
*
*    'VOCABULARY'
*
         HEX   8A
         DCI   'VOCABULARY'
         DA    VOCLINK-11
VOCABUL  DA    DOCOLON
         DA    TVOCABU
         DA    AT
         DA    EXECUTE
         DA    EXIT
*
*    'WARNING'  ( --- ADDR )
*
         HEX   87
         DCI   'WARNING'
         DA    VOCABUL-13
WARNING  DA    DOUSER
         HEX   0E
*
*    'WHILE'  ( F --- )
*
         HEX   C5
         DCI   'WHILE'
         DA    WARNING-10
WHILE    DA    DOCOLON
         DA    IF
         DA    TWOP
         DA    EXIT
*
*    'WHERE'  ( --- )
*
         HEX   85
         DCI   'WHERE'
         DA    WHILE-8
WHERE    DA    DOCOLON
         DA    BLK
         DA    AT
         DA    ZBRANCH
         DFB   WHERE1-*,00
         DA    BLK
         DA    AT
         DA    DUP
         DA    SCR
         DA    STORE
         DA    CR
         DA    CR
         DA    ADOTQA
         HEX   05
         ASC   'SCR# '
         DA    DUP
         DA    DOT
         DA    TOIN
         DA    AT
         DA    LIT
         HEX   FF03
         DA    MIN
         DA    CSLL
         DA    SLMOD
         DA    DUP
         DA    ADOTQA
         HEX   06
         ASC   'LINE# '
         DA    DOT
         DA    CSLL
         DA    STAR
         DA    ROT
         DA    BLOCK
         DA    PLUS
         DA    CR
         DA    CR
         DA    CSLL
         DA    DTRAIL
         DA    TYPE
         DA    TOIN
         DA    AT
         DA    LIT
         HEX   FF03
         DA    GREATER
         DA    PLUS
         DA    BRANCH
         DFB   WHERE2-*,00
WHERE1   DA    TOIN
         DA    AT
WHERE2   DA    CR
         DA    HERE
         DA    CAT
         DA    DUP
         DA    TOR
         DA    SUB
         DA    HERE
         DA    RAT
         DA    PLUS
         DA    ONEP
         DA    CAT
         DA    LIT
         HEX   2000
         DA    EQUAL
         DA    ZBRANCH
         DFB   WHERE3-*,00
         DA    ONEM
WHERE3   DA    SPACES
         DA    RFROM
         DA    ZERO
         DA    ADOA
WHERE4   DA    LIT
         HEX   5E00
         DA    EMIT
         DA    ALOOPA
         DFB   WHERE4-*,>WHERE4-*
         DA    EXIT
*
*    'WIDTH'  ( --- ADDR )
*
         HEX   85
         DCI   'WIDTH'
         DA    WHERE-8
WIDTH    DA    DOUSER
         HEX   0C
*
*    'WORD'  ( C --- ADDR )
*
         HEX   84
         DCI   'WORD'
         DA    WIDTH-8
WORD     DA    DOCOLON
         DA    TWORD
         DA    AT
         DA    EXECUTE
         DA    EXIT
*
*   'X'  NULL ( --- )
*
         HEX   C0         ZERO COUNT FOR 'WORD'
         HEX   80
         DA    WORD-7
NULL     DA    DOCOLON
         DA    BLK
         DA    AT
         DA    ZBRANCH
         DFB   NULL1-*,00
         DA    STATE
         DA    AT
         DA    QSTREAM
NULL1    DA    RFROM
         DA    DROP
         DA    EXIT
*
*    'XOR' ( N1 N2 --- XOR )
*
         HEX   83
         DCI   'XOR'
         DA    NULL-4
XOR      DA    XOR+2
         LDA   $0,X
         EOR   $2,X
         PHA
         LDA   $1,X
         EOR   $3,X
         INX
         INX
         JMP   PUT
*
*    '['  ( --- )
*
         HEX   C1
         ASC   "["
         DA    XOR-6
LBRAC    DA    DOCOLON
         DA    ZERO
         DA    STATE
         DA    STORE
         DA    EXIT
*
*    '[COMPILE]'
*
         HEX   C9
         DCI   '[COMPILE]'
         DA    LBRAC-4
BCOMPIL  DA    DOCOLON
         DA    QCOMP
         DA    DFIND
         DA    NOT
         DA    AABORTQ
         HEX   09
         ASC   'NOT FOUND'
         DA    DROP
         DA    CFA
         DA    COMMA
         DA    EXIT
*
*    ']'  ( --- )
*
         HEX   81
         ASC   "]"
         DA    BCOMPIL-12
RBRAC    DA    DOCOLON
         DA    LIT
         HEX   C000
         DA    STATE
         DA    STORE
         DA    EXIT
